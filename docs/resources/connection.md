---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "steampipecloud_connection Resource - terraform-provider-steampipecloud"
subcategory: ""
description: |-
  Provides a Steampipe Connection resource. The `Steampipe Cloud Connection` represents a set of tables for a single data source. Each connection is represented as a distinct Postgres schema. In order to query data, you'll need at least one connection.

  Connections are defined at the user account or organization level, and they can be shared by multiple workspaces within the account or organization.
---

# Resource: steampipecloud_connection

Manages a connection, which is defined at the user account or organization level.

## Example Usage

**Create an AWS connection (for a user)**

```hcl
resource "steampipecloud_connection" "aws_aaa" {
  plugin = "aws"
  handle = "aws_aaa"
  config = jsonencode({
    access_key = "redacted"
    secret_key = "redacted"
    regions    = ["us-east-1"]
  })
}
```

**Create an AWS connection (for an organization)**

```hcl
resource "steampipecloud_connection" "aws_aab" {
  organization = "myorg"
  plugin       = "aws"
  handle       = "aws_aab"
  config = jsonencode({
    access_key = "redacted"
    secret_key = "redacted"
    regions    = ["us-east-1"]
  })
}
```

**Create an AWS connection using IAM role mode**

This example requires the AWS provider, but if you have an existing role, you
can just create the `steampipecloud_connection` resource.

```hcl
data "steampipecloud_user" "caller" {}

resource "random_string" "random" {
  length  = 8
  special = false
  upper   = false
}

locals {
  # The format of external id is ^(u|o)_[a-z0-9_]{20}:[a-z0-9]{8}$`
  external_id = "${data.steampipecloud_user.caller.user_id}:${random_string.random.id}"
}

provider "aws" {
  region = "us-east-1"
}

# Create AWS IAM role
resource "aws_iam_role" "steampipe_cloud_role" {
  name = "steampipe_cloud"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          AWS = "arn:aws:iam::123456789012:root"
        },
        Condition = {
          StringEquals = {
            "sts:ExternalId" = local.external_id
          }
        }
      },
    ]
  })
}

data "aws_iam_policy" "ReadOnlyAccess" {
  arn = "arn:aws:iam::aws:policy/ReadOnlyAccess"
}

# Attach ReadOnlyAccess policy to role
resource "aws_iam_role_policy_attachment" "steampipe_cloud_role_attach" {
  role       = aws_iam_role.steampipe_cloud_role.name
  policy_arn = data.aws_iam_policy.ReadOnlyAccess.arn
}

# Create connection with IAM role
resource "steampipecloud_connection" "aws_role_connection" {
  handle      = "aws_role_connection"
  plugin      = "aws"
  config = jsonencode({
    regions     = ["us-east-1", "us-east-2"]
    role_arn    = aws_iam_role.steampipe_cloud_role.arn
    external_id = local.external_id
  })
}
```

**Create a GCP connection**

```hcl
resource "steampipecloud_connection" "gcp_aaa" {
  handle      = "gcp_aaa"
  plugin      = "gcp"
  config = jsonencode({
    project     = "project-aaa"
    credentials = file("/Users/myuser/Downloads/project-aaa.json")
  })
}
```

**Create an OCI connection**

```hcl
resource "steampipecloud_connection" "oci_aaa" {
  handle       = "oci"
  plugin       = "oci"
  config = jsonencode({
    user_ocid    = "ocid1.user.oc1..aaaaaaaaw..."
    fingerprint  = "f1:fc:44:3a:..."
    tenancy_ocid = "ocid1.tenancy.oc1..aaaaaaaah..."
    regions      = ["ap-mumbai-1", "us-ashburn-1"]
    private_key  = file("/Users/myuser/Downloads/mykey.cer")
  })
}
```

## Argument Reference

The following arguments are supported:

- `handle` - (Required) A friendly identifier for your connection, and must be unique across your connections.
- `plugin` - (Required) The name of the plugin.
- `organization` - (Optional) An organization ID or handle to create the connection in.
- `config` - (Optional) Configuration for the connection.

For each connection resource, additional arguments are supported based on the plugin it uses. For instance, if creating a connection that uses the Zendesk plugin, the [Zendesk configuration arguments](https://hub.steampipe.io/plugins/turbot/zendesk#configuration) should be used in the connection:

```hcl
resource "steampipecloud_connection" "zendesk" {
  plugin    = "zendesk"
  handle    = "zendesk_example"
  config = jsonencode({
    subdomain = "dmi"
    email     = "pam@dmi.com"
    token     = "17ImlCYdfZ3WJIrGk96gCpJn1fi1pLwexample"
  })
}
```

We do not recommend storing sensitive or secret information in such a way that it's accidentally exposed or can be accessed by unwanted actors. For more information on how to protect sensitive information, please see [Protect Sensitive Input Variables](https://learn.hashicorp.com/tutorials/terraform/sensitive-variables).

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

- `connection_id` - An unique identifier of the connection.
- `identity_id` - A unique identifier of the entity where the connection is created.
- `type` - The type of the resource.

## Import

### Import User Connection

User connections can be imported using the connection `handle`, e.g.,

```sh
terraform import steampipecloud_connection.example aws_aaa
```

### Import Organization Connection

Organization connections can be imported using an ID made up of `organization_handle:connection_handle`, e.g.,

```sh
terraform import steampipecloud_connection.example myorg:aws_aab
```
